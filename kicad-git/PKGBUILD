# Maintainer: Kyle Keen <keenerd@gmail.com>, Binghoo Dang <dangbinghoo@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad-git
_gitname=kicad
pkgver=7280.53b8a8c
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('glew' 'wxgtk' 'hicolor-icon-theme' 'desktop-file-utils' 'boost-libs')
makedepends=('cmake' 'kicad' 'zlib' 'mesa' 'boost')
optdepends=('kicad-docs-bzr: for documentation' 
            'kicad-library-bzr: for footprints'
            'git: github pcb plugin')
conflicts=('kicad')
provides=('kicad')
install=kicad.install
source=('kicad::git://github.com/KiCad/kicad-source-mirror.git#branch=master')
md5sums=('SKIP')

# mkdir -p "$pkgdir/etc/profile.d"
# echo "export KIGITHUB=https://github.com/KiCad" > "$pkgdir/etc/profile.d/kicad.sh"
# https://github.com/blairbonnett-mirrors/kicad/blob/master/scripts/kicad-install.sh

pkgver() {
   cd "${_gitname}"
   echo $(git rev-list --count master).$(git rev-parse --short master)
}


build() {
  cd "$srcdir/${_gitname}"
  mkdir -p build/Release
  cd build/Release
  # -DKICAD_SKIP_BOOST=ON ?
  # -DKICAD_SCRIPTING_WXPYTHON=ON ?
  cmake ../.. -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DKICAD_SKIP_BOOST=ON \
	      -DKICAD_SCRIPTING=ON \
	      -DKICAD_SCRIPTING_MODULES=ON \
              -DBUILD_GITHUB_PLUGIN=ON
  make #-j1
}

package() {
  cd "$srcdir/${_gitname}/build/Release"
  make DESTDIR="$pkgdir" install
}
